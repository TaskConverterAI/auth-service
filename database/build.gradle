buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.7.5'
        classpath 'org.flywaydb:flyway-database-postgresql:11.3.2'
    }
}

plugins {
    id 'org.flywaydb.flyway' version '11.3.2'
    id 'com.avast.gradle.docker-compose' version '0.17.5'
}

ext {
    dbName = env.allVariables.getOrDefault("PG_DB_NAME", "postgres")
    dbUser = env.allVariables.getOrDefault("PG_USERNAME", "postgres")
    dbPassword = env.allVariables.getOrDefault("PG_PASSWORD", "postgres")
}

dockerCompose {
    environment.put "PG_DB_NAME", dbName
    environment.put "PG_USERNAME", dbUser
    environment.put "PG_PASSWORD", dbPassword
}

flyway {
    url = "jdbc:postgresql://localhost:5433/${dbName}"
    user = dbUser
    password = dbPassword
    locations = ['filesystem:scripts/migrations']
    cleanDisabled = false
}

task flywayCleanAndMigrate(dependsOn: ['flywayClean', 'flywayMigrate']) {
    group = 'Flyway'
    description = 'Clean the database and run all migrations'

    tasks.findByName('flywayMigrate').mustRunAfter 'flywayClean'
}
